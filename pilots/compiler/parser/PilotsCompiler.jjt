PARSER_BEGIN(PilotsCompiler)
import java.io.*;

public class PilotsCompiler {
    public static void main(String args[]) {
        try {
            PilotsCompiler parser = new PilotsCompiler( new FileReader(args[0]) );
            SimpleNode rootNode = parser.Start();
            rootNode.dump(" ");
        } catch ( Exception ex ) {
            System.err.println( "Failed to parse : " + ex.getMessage() );
        }
    }
}
PARSER_END(PilotsCompiler)

/***************************************/
/********** TOKEN DEFINITIONS **********/
/***************************************/

/*** Skip whitespace and comments ***/
SKIP : {
" "
| "\t"
| "\n"
| "\r"
| "\r\n"
}

/*** The keywords ***/
TOKEN : {
<PROGRAM_START : "program">
| <INPUTS: "inputs">
| <OUTPUTS: "outputs">
| <ERRORS: "errors">
| <USING: "using">
| <AT: "at">
| <EVERY: "every">
| <CLOSEST: "closest">
| <EUCLIDEAN: "euclidean">
| <INTERPOLATE: "interpolate">
| <NSEC: "nsec">
| <USEC: "usec">
| <MSEC: "msec">
| <SEC: "sec">
| <MIN: "min">
| <HOUR: "hour">
| <DAY: "day">
| <SQRT: "sqrt">
| <SIN: "sin">
| <COS: "cos">
| <TAN: "tan">
| <ARCSIN: "arcsin">
| <ARCCOS: "arccos">
| <ARCTAN: "arctan">
| <ABS: "abs">
| <PROGRAM_END: "end">
}

TOKEN : {
<#DIGIT : ["0"-"9"]>
| <LPAR: "(">
| <RPAR: ")">
| <COMMA: ",">
| <INTEGER : ("+" | "-")? (<DIGIT>)+>
| <#SCALEFACTOR : ("E" | "e") ("+" | "-")? (<DIGIT>)+>
| <REAL : ("+" | "-")? (<DIGIT>)+ "." (<DIGIT>)* (<SCALEFACTOR>)?>
| <#LETTER : ["A"-"Z","a"-"z","_"]>
| <VAR : <LETTER> (<LETTER> | <DIGIT>)*>
}


/**************************************/
/********** START OF GRAMMAR **********/
/**************************************/

SimpleNode Start(): {}
{
    <PROGRAM_START> <VAR> ";"
        <INPUTS> (";" | (Input())*)
        <OUTPUTS> (";" | (Output())*)
        <ERRORS> (";" | (Output())*)
    <PROGRAM_END> ";"
    { return jjtThis; }
}

void Input(): {}
{
    Vars() ":" Dims() <USING> Methods() ";"
}

void Output(): {}
{
    Vars() ":" Exps() <AT> <EVERY> Time() ";"
}

void Vars(): {}
{
    <VAR> ("," <VAR>)*
}

void Dims(): {}
{
    // "(t)" | "(x,t)" | "(x,y,t)" | "(x,y,z,t)"
    // "(" ["x," ["y,"["z,"]]] "t" ")"
    // "(" ["x," ["y,"["z,"]]] <VAR> ")"
    "(" Exps() ")"      // workaround to avoid conflicts with the arguments for Method()
}

void Method(): {}
{
    (<CLOSEST> | <EUCLIDEAN> | <INTERPOLATE>) "(" Exps() ")"
}

void Methods(): {}
{
    Method() ("," Method())*
}

void Time(): {}
{
    Number() (<NSEC> | <USEC> | <MSEC> | <SEC> | <MIN> | <HOUR> | <DAY>)
}

void Exps(): {}
{
    Exp() ("," Exp())*
}

void Exp(): {}
{
    Func() "(" Exps() ")" Exp2() | "(" Exp() ")" Exp2() | Value() Exp2()
}

void Exp2(): {}
{
    Func() Exp() Exp2() | {}
}

void Func(): {}
{
    "+" | "-" | "*" | "/" | "^" | 
    <SQRT> | <SIN> | <COS> | <TAN> | <ARCSIN> | <ARCCOS> | <ARCTAN> | <ABS>
}

void Number(): {}
{
    <INTEGER> | <REAL>
}

void Value(): {}
{
    Number() | <VAR>
}
